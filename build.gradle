plugins {
    id 'java'
    id 'de.undercouch.download' version '5.1.0'
    id 'idea'
}

sourceSets {
    main {
         java {
            srcDirs = ['src']
         }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation(platform('org.junit:junit-bom:5.9.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')

    implementation files('lib/algs4.jar')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('downloadZipFile', Download) {
    src 'https://algs4.cs.princeton.edu/code/algs4-data.zip'
    dest new File('algs4-data.zip')
    overwrite false
}

tasks.register('downloadAndUnzipTestDataFile', Copy) {
    dependsOn downloadZipFile
    from zipTree(downloadZipFile.dest)
    into new File('./')
}

tasks.register('downloadLib', Download) {
    src 'https://algs4.cs.princeton.edu/code/algs4.jar'
    dest new File('lib/algs4.jar')
    overwrite false
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
